// Example gradle script to show how a bug tracker plugin can be build

apply plugin: 'java'
apply plugin: 'osgi'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'
version = '21.2.0.0143'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly fileTree(dir: 'lib', include: 'fortify-public*.jar')
    implementation(group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.5')
    implementation(group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.5')
    implementation(group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.4.9')
    implementation(group: 'commons-codec', name: 'commons-codec', version: '1.6')
    implementation(group: 'commons-logging', name: 'commons-logging', version: '1.2')
    implementation(group: 'commons-lang', name: 'commons-lang', version: '2.4')

    implementation("javax.xml.bind:jaxb-api:2.3.0")
    runtimeOnly("com.sun.activation:javax.activation:1.2.0")
    runtimeOnly("org.glassfish.jaxb:jaxb-runtime:2.2.11")

    testImplementation(group: 'org.junit', name: 'junit', version: '4.8.2')
}

jar.enabled = false // We don't need to generate a default non-osgi jar during build

clean {
    delete "${projectDir}/dist"
}

def bundleClassPath() {
    def list = ['.']
    configurations.runtimeClasspath.findAll {
        !it.name.contains('source-') && !it.name.contains('javadoc-')
    }.sort{ it.name }.each {
        list += 'lib/' + it.name
    }
    return list.join(',')
}

ext {
    pluginManifest = osgiManifest {
        classesDir = sourceSets.main.output.classesDirs.singleFile
        classpath = configurations.runtimeClasspath

        instructionReplace 'Bundle-Name', '%Bundle-Name'
        instruction 'Bundle-Vendor', '%Bundle-Vendor'
        instruction 'Bundle-ActivationPolicy', 'lazy'
        instruction 'Bundle-Localization', 'plugin'
        instruction 'Bundle-RequiredExecutionEnvironment', 'JavaSE-1.8'
        instruction 'Built-By', 'Fortify'
        instruction 'Version', project.version
        instruction 'Bundle-ClassPath', bundleClassPath()

        instructionReplace 'Bundle-SymbolicName', 'com.hp.fortify.BugTrackerPluginAlm;singleton:=true'
        instructionReplace 'Bundle-Version', project.version
        instructionReplace 'Require-Bundle', 'com.fortify.common.public-api'    // Needed by AWB
        instructionReplace 'Import-Package', '!*'   // ..suppress the bndTools default import generation
        instructionReplace 'Export-Package', 'com.fortify.sample.bugtracker.alm', 'com.fortify.sample.bugtracker.alm.infra'
    }
}

task pluginJar(type: Jar) {
    baseName "com.fortify.sample.BugTrackerPluginAlm"
	from sourceSets.main.output
    destinationDir = file("${projectDir}/dist")
    manifest = pluginManifest
    from(projectDir) {
        include "plugin.properties"
        include "plugin.xml"
    }
    into("lib") {
        from configurations.runtimeClasspath.files
        include "*.jar"
    }
}

build.dependsOn(pluginJar)
